plugins {
    id("org.kordamp.gradle.jdeps") version "0.20.0" apply false
    id("org.kordamp.gradle.jdeprscan") version "0.11.0" apply false
}

allprojects {
    version = "1.0.0"
    apply plugin: "java-library"
}

if (!JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)) {
    throw new GradleException("Java 17 or later is required to build Java-tron.\n" +
            "  Detected version ${JavaVersion.current()}")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
        vendor = JvmVendorSpec.ADOPTIUM
    }
}

subprojects {
    apply plugin: "jacoco"
    apply plugin: "maven-publish"

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    jacoco {
        toolVersion = "0.8.10"
    }
    buildscript {
        repositories {
            mavenCentral()
            maven { url 'https://jitpack.io' }
            maven {
                url "https://plugins.gradle.org/m2/"
            }
        }
        dependencies {
            classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.1'
            classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://repo.spring.io/plugins-release' }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        implementation group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.25'
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.6'
        implementation group: 'com.google.guava', name: 'guava', version: '33.2.1-jre'
        implementation "com.google.code.findbugs:jsr305:3.0.2"
        implementation group: 'org.springframework', name: 'spring-context', version: '5.3.37'
        implementation group: 'org.springframework', name: 'spring-tx', version: '5.3.37'
        implementation "org.apache.commons:commons-lang3:3.4"
        implementation group: 'org.apache.commons', name: 'commons-math', version: '2.2'
        implementation "org.apache.commons:commons-collections4:4.1"
        implementation group: 'joda-time', name: 'joda-time', version: '2.3'
        implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.69'

        compileOnly 'org.projectlombok:lombok:1.18.34'
        annotationProcessor 'org.projectlombok:lombok:1.18.34'
        testCompileOnly 'org.projectlombok:lombok:1.18.34'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'

        // https://www.oracle.com/java/technologies/javase/11-relnote-issues.html#JDK-8190378
        implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
        implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
        implementation group: 'javax.jws', name: 'javax.jws-api', version: '1.1'
        implementation group: 'javax.activation', name: 'activation', version: '1.1.1'
        implementation group: 'javax.resource', name: 'javax.resource-api', version: '1.7.1'
        implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
        implementation group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1.1'
        implementation group: 'javax.ejb', name: 'ejb-api', version: '3.0'
        implementation group: 'javax.enterprise.concurrent', name: 'javax.enterprise.concurrent-api', version: '1.1'
        implementation group: 'javax.money', name: 'money-api', version: '1.1'
        implementation group: 'javax.enterprise', name: 'cdi-api', version: '1.2'
        implementation group: 'javax.portlet', name: 'portlet-api', version: '3.0.1'
        implementation group: 'javax.xml.ws', name: 'jaxws-api', version: '2.3.1'
        implementation group: 'javax.xml.rpc', name: 'javax.xml.rpc-api', version: '1.1.2'
        annotationProcessor group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
        annotationProcessor group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'


        testImplementation group: 'junit', name: 'junit', version: '4.13.2'
        testImplementation "org.mockito:mockito-core:5.8.0"
    }

    tasks.register('sourcesJar', Jar) {
        dependsOn classes
        classifier = "sources"
        from sourceSets.main.allSource
        duplicatesStrategy = DuplicatesStrategy.INCLUDE // allow duplicates
    }


    tasks.withType(AbstractArchiveTask).configureEach {
        preserveFileTimestamps = false
        reproducibleFileOrder = true
        duplicatesStrategy = DuplicatesStrategy.INCLUDE // allow duplicates
    }

    configurations.configureEach {
        resolutionStrategy {
            force group: 'com.google.guava', name: 'guava', version: '33.2.1-jre'
        }
    }
}

tasks.register('copyToParent', Copy) {
    into "$buildDir/libs"
    subprojects {
        from tasks.withType(Jar)
    }
}

build.finalizedBy(copyToParent)

gradle.buildFinished {
    if (project.hasProperty('cleanSubBuild')) {
        subprojects {
            buildDir.deleteDir()
        }
    }
}
